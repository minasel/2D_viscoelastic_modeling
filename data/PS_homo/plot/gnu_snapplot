# gnu_snapplot
unset key
set macros
c=5
set terminal postscript eps size 35.0/c,17.0/c font "Arial, 16"
set output sprintf("snap.eps")
set format cb "%1.2f"
### set color
set palette file "-"
0 0 1
1 1 1
1 0 0
e
set yrange [1:256] reverse
NOTICS="unset title; unset xlabel;unset ylabel;unset xtics;unset ytics; unset x2tics; unset y2tics; unset colorbox"
TICS="set xlabel 'X position (km)'; set ylabel 'Depth (km)'; set xtics('0.0' 0.0,'1.0' 64.0,'2.0' 128.0,'3.0' 192.0,'4.0' 255.0) in; set ytics('0.0' 0,'1.0' 64.0,'2.0' 128.0,'3.0' 192.0,'4.0' 255.0) in; set grid front; unset grid"
vsize=0.35
hsize=0.17
lmarg=0.07
tmarg=0.88
mmarg=0.08
TMARGIN = "set tmargin at screen tmarg; set bmargin at screen tmarg-vsize"
BMARGIN = "set tmargin at screen tmarg-vsize-mmarg; set bmargin at screen tmarg-vsize-mmarg-vsize"
LMARGIN1 = "set lmargin at screen lmarg; set rmargin at screen lmarg+hsize"
LMARGIN2 = "set lmargin at screen lmarg+hsize*1; set rmargin at screen lmarg+hsize*2"
LMARGIN3 = "set lmargin at screen lmarg+hsize*2; set rmargin at screen lmarg+hsize*3"
LMARGIN4 = "set lmargin at screen lmarg+hsize*3; set rmargin at screen lmarg+hsize*4"
LMARGIN5 = "set lmargin at screen lmarg+hsize*4; set rmargin at screen lmarg+hsize*5"

@NOTICS
### plot
r=0.02 #for vector 
set cbrange [-1*r:1*r]
set multiplot layout 2,5 rowsfirst
@TICS;unset xlabel; unset xtics;
@TMARGIN; @LMARGIN1
set title "T=55 ms"
plot "snap_h_1" binary array=256x256 scan=yx format="%float" with image

@NOTICS
@TMARGIN; @LMARGIN2
set title "T=110 ms"
plot "snap_h_2" binary array=256x256 scan=yx format="%float" with image
@TMARGIN; @LMARGIN3
set title "T=165 ms"
plot "snap_h_3" binary array=256x256 scan=yx format="%float" with image
@TMARGIN; @LMARGIN4
set title "T=220 ms"
plot "snap_h_4" binary array=256x256 scan=yx format="%float" with image
@TMARGIN; @LMARGIN5
set cbtics('-2.0' -r,'-1.0' -r/2,'0.0' 0.0,'1.0' r/2,'2.0' r) 
set title "T= 275 ms"
plot "snap_h_5" binary array=256x256 scan=yx format="%float" with image
#
set cbrange [-1*r:1*r]

@NOTICS
@TICS;
@BMARGIN; @LMARGIN1
plot "snap_v_1" binary array=256x256 scan=yx format="%float" with image
@NOTICS
@BMARGIN; @LMARGIN2
plot "snap_v_2" binary array=256x256 scan=yx format="%float" with image
@BMARGIN; @LMARGIN3
plot "snap_v_3" binary array=256x256 scan=yx format="%float" with image
@BMARGIN; @LMARGIN4
plot "snap_v_4" binary array=256x256 scan=yx format="%float" with image
@BMARGIN; @LMARGIN5
set cbtics('-2.0' -r,'-1.0' -r/2,'0.0' 0.0,'1.0' r/2,'2.0' r) 
set label 1 'Horizontal'  at screen 0.46, 0.97 
set label 2 'Vertical'  at screen  0.47, 0.49
plot "snap_v_5" binary array=256x256 scan=yx format="%float" with image
### delete temp files
